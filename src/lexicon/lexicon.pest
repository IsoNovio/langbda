WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE* }

lexicon = _{ SOI ~ section* ~ EOI }
section = _{ feature_section | functional_section | lexical_section }

feature_section = { "[Features]" ~ NEWLINE* ~ feature_entry* }
feature_entry = { feature_category ~ "=" ~ feature_values ~ NEWLINE* }
feature_category = { ASCII_ALPHANUMERIC+ }
feature_values = { feature_value ~ ("," ~ feature_value)* }
feature_value = { ASCII_ALPHANUMERIC+ }

feature_set = { feature ~ ("-" ~ feature)* }
feature = { ASCII_ALPHANUMERIC+ }

functional_section = { "[Functional]" ~ NEWLINE* ~ functional_entry* }
functional_entry = { feature_set ~ "=" ~ interpretation ~ NEWLINE* }
interpretation = _{ right_projection | left_projection | feature_set }

left_projection = { feature_set ~ ">" ~ feature_set }
right_projection = { feature_set ~ ">>" ~ feature_set }

lexical_section = { "[Lexical]" ~ NEWLINE* ~ lexical_entry* }
lexical_entry = { lexical_item ~ "=" ~ feature_set ~ NEWLINE* }
lexical_item = { (ASCII_ALPHA | PUNCTUATION)+ }
